apply plugin: 'com.android.application'

def buildTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddhhmmss')
    return formattedDate
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.wangcai.lottery"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 7
        versionName "7.0.0"
        flavorDimensions "default"
        // AndroidManifest.xml 里面CHANNEL的value为 ${CHANNEL_VALUE}
        manifestPlaceholders = [WANGCAI_CHANNEL_VALUE: "app_name"]
    }

    //执行lint检查，有任何的错误或者警告提示，都会终止构建，我们可以将其关掉。
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        // 防止在发布的时候出现因MissingTranslation导致Build Failed!
        disable 'MissingTranslation'
    }

    //签名
    signingConfigs {
        debug {
        }
        relealse {
            storeFile file('C:\\0\\开发环境\\garble_jpg\\com.wangcai.jks')
            storePassword '123qwe'
            keyAlias '25'
            keyPassword '123qwe'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.relealse
        }

        debug {
            minifyEnabled false
        }
    }

    //渠道Flavors
    productFlavors {
        pre {
            dimension "default"
            resValue "string", "app_name", "旺彩"
            manifestPlaceholders = [WANGCAI_CHANNEL_VALUE: "WANGCAI_RELEASE"]
        }
        trial{
            dimension "default"
            resValue "string", "app_name", "旺彩"
            manifestPlaceholders = [WANGCAI_CHANNEL_VALUE: "WANGCAI_TEST"]
        }
    }

    //Java版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // 替换manifest中定义的占位符
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [WANGCAI_CHANNEL_VALUE: name]
    }

    //自动生成指定名称的release发布版的 apk文件
    android.applicationVariants.all { variant ->
        def appName
        def time = "-${buildTime()}.apk"
        def fileName
        variant.outputs.each { output ->
            fileName = output.name.replace("${variant.flavorName}", "V${defaultConfig.versionName}-${variant.flavorName}")
        }

        if (project.hasProperty("applicationName")) {
            appName = applicationName
        } else {
            appName = parent.name
        }

        variant.outputs.all { output ->
            outputFileName = appName + "-" + fileName + time
        }
    }
}


configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.0.2'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.0.2'
    implementation 'com.android.support:design:26.0.2'
    implementation 'com.android.support:cardview-v7:26.0.2'
    implementation 'com.android.support:support-v4:26.0.2'
    implementation 'com.android.support:palette-v7:26.0.2'
    implementation 'com.android.support:recyclerview-v7:26.0.2'
    //网络工具类
    implementation 'com.mcxiaoke.volley:library:1.0.8'
    //json 工具类
    implementation 'com.google.code.gson:gson:2.8.2'
    //状态栏的工具类
    implementation 'com.jaeger.statusbarutil:library:+'
    //切换时共享视图元素的切换动画
    implementation 'com.kogitune:pre-lollipop-activity-transition:1.0.0'
    //View注入框架
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //圆形图片
    implementation 'com.github.thinkcool:circletextimageview:1.0.20151218'
    //android 权限工具类
    implementation 'com.github.hotchemi:permissionsdispatcher:2.3.1'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.3.1'
    //扫描二维码
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.google.zxing:android-core:3.3.0'
    //图片加载库
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.github.bumptech.glide:volley-integration:1.4.0@aar'
    //云标签
    implementation 'co.lujun:androidtagview:1.1.4'
    //友盟
    implementation 'com.umeng.analytics:analytics:latest.integration'
    implementation 'com.google.android.gms:play-services-appindexing:8.4.0'
    //下拉刷新
    implementation 'in.srain.cube:ultra-ptr:1.0.11'
    implementation 'in.srain.cube:cube-sdk:1.0.44@aar'
    implementation 'in.srain.cube:clog:1.0.2'

    implementation 'com.github.gzu-liyujiang.AndroidPicker:WheelPicker:1.5.5'
    //沉浸式状态栏和沉浸式导航栏管理
    implementation 'com.gyf.barlibrary:barlibrary:2.3.0'
    implementation 'com.github.michaelye.easydialog:easydialog:1.4'

    implementation 'cn.bingoogolapple:bga-badgeview-api:1.1.8'
    annotationProcessor 'cn.bingoogolapple:bga-badgeview-compiler:1.1.8'
    implementation 'com.github.cpiz:BubbleView:1.0.2'
    implementation 'q.rorbin:badgeview:1.1.3'
    implementation 'com.github.ceryle:RadioRealButton:v2.1.1'
}
